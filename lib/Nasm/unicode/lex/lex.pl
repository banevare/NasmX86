#!/usr/bin/perl -I/home/phil/perl/cpan/DataTableText/lib/
#-------------------------------------------------------------------------------
# Find all 13 Unicode Mathematical Alphabets as used by Perl Zero.
# Philip R Brenan at appaapps dot com, Appa Apps Ltd Inc., 2021
#-------------------------------------------------------------------------------
use warnings FATAL => qw(all);
use strict;
use Carp;
use Data::Dump qw(dump);
use Data::Table::Text qw(:all);
use Test::More qw(no_plan);
use Tree::Term;
use feature qw(say state current_sub);
use utf8;

my $home    = currentDirectory;                                                 # Home folder
my $unicode = q(https://www.unicode.org/Public/UCD/latest/ucd/UnicodeData.txt); # Unicode specification
my $data    = fpe $home, qw(unicode txt);                                       # Local copy of unicode
my $lexicalsFile = fpe $home, qw(lex data);                                     # Dump of lexicals

=pod

Space processing - remove leading and trailing spaces
New line as semi colon when new line occurs after a variable
Alphabet compression so we can make better use of Tree:Multi
Text generation routine let us write some pretend code to parse

=cut

my $bracketBase = 0x10;                                                         # Start numbering brackets from here

sub LexicalConstant($$;$)                                                       # Lexical constants as opposed to derived values
 {my ($name, $number, $letter, $like) = @_;                                     # Name of the lexical item, numeric code as used in Nida, character code as used Tree::Term, a specialized instance of this Tree::Term which is never the less lexically identical to the Tree::Term
  genHash("Nida::Lexical::Constant",                                            # Description of a lexical item connecting the definition in Tree::Term with that in Nida::Lexicals
    name   => $name,                                                            #I Name of the lexical item
    number => $number,                                                          #I Numeric code for lexical item
    letter => $letter,                                                          #I Alphabetic name for lexical item
   );
 }

my $Lexicals = genHash("Nida::Lexicals",                                        # Lexical items
  OpenBracket      => LexicalConstant("OpenBracket",       0, 'b'),             # The lowest bit of an open bracket code is zero
  CloseBracket     => LexicalConstant("CloseBracket",      1, 'B'),             # The lowest bit of a close bracket code is one
  Ascii            => LexicalConstant("Ascii",             2, 'v'),             # Ascii characters
  NewLine          => LexicalConstant("NewLine",           3, 'v'),             # New line character
  dyad             => LexicalConstant("dyad",              4, 'd'),             # Infix operator with left to right binding at priority 3
  prefix           => LexicalConstant("prefix",            5, 'p'),             # Prefix operator - it applies only to the following variable
  assign           => LexicalConstant("assign",            6, 'a'),             # Assign infix operator with right to left binding at priority 2.
  variable         => LexicalConstant("variable",          7, 'v'),             # Variable although it could also be an ascii string or regular expression
  suffix           => LexicalConstant("suffix",            8, 'q'),             # Suffix operator - it applies only to the preceding variable
  semiColon        => LexicalConstant("semiColon",         9, 's'),             # Infix operator with left to right binding at priority 1
  NewLineSemiColon => LexicalConstant("NewLineSemiColon", 10),                  # A new line character that is also acting as a semi colon
  WhiteSpace       => LexicalConstant("WhiteSpace",       11),                  # White space not between non ascii items
  term             => LexicalConstant("term",             12),                  # Term in the parse tree
  empty            => LexicalConstant("empty",            13),                  # Empty term present between two adjacent semicolons
 );

my $TreeTermLexicals = genHash("Nida::TreeTermLexicals",                        # Tree Term Lexical items embodied as Nida lexical items
  a => "assign",
  d => "dyad",
  b => "OpenBracket",
  B => "CloseBracket",
  p => "prefix",
  s => "semiColon",
  q => "suffix",
  t => "term",
  v => "variable",
 );

my $Tables = genHash("Nida::Lexical::Tables",                                   # Tables used to parse lexical items
  alphabets        => undef,                                                    # Alphabets selected from uncode database
  alphabetRanges   => undef,                                                    # Number of alphabet ranges
  brackets         => undef,                                                    # Number of brackets
  bracketsHigh     => undef,                                                    # High zmm for closing brackets
  bracketsLow      => undef,                                                    # Low  zmm for opening brackets
  bracketsOpen     => undef,                                                    # Open brackets
  bracketsClose    => undef,                                                    # Close brackets
  lexicalAlpha     => undef,                                                    # The alphabets assigned to each lexical item
  lexicalHigh      => undef,                                                    # High zmm for lexical items
  lexicalLow       => undef,                                                    # Low  zmm for lexical items
  lexicals         => $Lexicals,                                                # The lexical items
  sampleText       => undef,                                                    # A sample Nida program
  transitions      => undef,                                                    # Zmm of transition possibilities
  treeTermLexicals => $TreeTermLexicals,                                        # Tree term lexicals
  semiColon        => q(‚ü¢),                                                     # Semi colon symbol, left star: U+27E2
  separator        => q(‚Åü),                                                     # Space for separating non ascii items: U+205F
 );

if (!-e $data)                                                                  # Download specification
 {say STDERR qx(curl -o $data $unicode);
 }

sub convert($)                                                                  # Convert a character from hex to actual
 {my ($c) = @_;                                                                 # Parameters
  eval "chr(0x$c)";                                                             # Character
 }

sub alphabets                                                                   # Locate the mathematical alphabets
 {my @s = readFile $data;

  my %alpha;                                                                    # Alphabet names

  for my $s(@s)                                                                 # Select the brackets we want
   {my @w = split /;/, $s;

# 1D49C;MATHEMATICAL SCRIPT CAPITAL A;Lu;0;L;<font> 0041;;;;N;;;;;
# 1D72D;MATHEMATICAL BOLD ITALIC CAPITAL THETA SYMBOL;Lu;0;L;<font> 03F4;;;;N;;;;;
# 1D70D;MATHEMATICAL ITALIC SMALL FINAL SIGMA;Ll;0;L;<font> 03C2;;;;N;;;;;

    my ($c, $d, $t) = @w;                                                       # Parse unicode specification
    my $C = convert $c;                                                         # Character

    next if     $d =~ m(DIGAMMA)i;                                              # Select family of letters
    next unless $d =~ m(\A(Mathematical|Squared|Circled|Negative))i;            # Select family of letters
    next unless $t =~ m(\A(L|So|Sm));                                           # Letter or other symbol

    my $D = $d;
       $D =~ s(PARTIAL DIFFERENTIAL) ( )igs;
       $D =~ s(NABLA)                ( )igs;
       $D =~ s(\w+ SYMBOL\Z)         ( )ig;
       $D =~ s(FINAL \w+\Z)          ( )ig;
       $D =~ s(\w+\Z)                ( )gs;
       $D =~ s(Capital)              ( )igs;
       $D =~ s(Small)                ( )igs;
       $D =~ s(\s+)                  ( )igs;

    $alpha{$D}{$c} = $C;                                                        # Place into alphabets
   }

  my %selected = (semiColon => $Tables->semiColon);                             # We cannot use semi colon as it is an ascii character, so we use this character instead  U+27E2

  for my $a(sort keys %alpha)
   {next unless $a =~ m((CIRCLED LATIN LETTER|MATHEMATICAL BOLD|MATHEMATICAL BOLD FRAKTUR|MATHEMATICAL BOLD ITALIC|MATHEMATICAL BOLD SCRIPT|MATHEMATICAL DOUBLE-STRUCK|MATHEMATICAL FRAKTUR|MATHEMATICAL ITALIC|MATHEMATICAL MONOSPACE|MATHEMATICAL SANS-SERIF|MATHEMATICAL SANS-SERIF BOLD|MATHEMATICAL SANS-SERIF|BOLD ITALIC|MATHEMATICAL SANS-SERIF ITALIC|MATHEMATICAL SCRIPT|NEGATIVE|CIRCLED LATIN LETTER|NEGATIVE SQUARED LATIN LETTER|SQUARED LATIN LETTER))i;
                                                                                # Selected alphabets
    my @l;
    for my $l(sort keys $alpha{$a}->%*)                                         # Sort alphabet by point
     {push @l, $alpha{$a}{$l};
     }
    my $l = join '', sort @l;                                                   # Alphabet

    next unless length($l) > 5;                                                 # Ignore short sets which are probably not alphabets
    my $A = lcfirst join '', map {ucfirst} split /\s+/, lc $a;
    $selected{$A} = $l;                                                         # Selected alphabets
   }

  #lll "AAAA", dump(\%alpha);                                                   # Alphabets discovered

  my @range;  my @zmm;                                                          # Ranges of characters

  for my $a(sort keys %selected)                                                # Print selected alphabets in ranges
   {my $z = '';
       $z = q(variable) if $a =~ m/mathematicalSans-serifBold\Z/;
       $z = q(dyad)     if $a =~ m/mathematicalBold\Z/;
       $z = q(prefix)   if $a =~ m/mathematicalBoldItalic\Z/;
       $z = q(assign)   if $a =~ m/mathematicalItalic\Z/;
       $z = q(suffix)   if $a =~ m/mathematicalSans-serifBoldItalic\Z/;
       $z = q(Ascii)    if $a =~ m/negativeCircledLatinLetter\Z/;               # Control characters as used in regular expressions and quoted strings

    push $Tables->lexicalAlpha->{$z}->@*, $a;                                   # Alphabets assigned to each lexical item

    my $Z = $z ? pad(" = $z", 16) : '';
    if ($z)                                                                     # Alphabet we are going to use for a lexical item
     {say STDERR '-' x 44;
      say STDERR pad("$a$Z", 42), " : ", $selected{$a};
      say STDERR '-' x 44;
     }
    else
     {say STDERR pad("$a$Z", 42), " : ", $selected{$a};
     }

    my @c = split //, $selected{$a};                                            # Divide selected alphabets into contiguous ranges
    push my @r, [shift @c];

    for my $c(@c)
     {my $b = ord($r[-1][-1]);
      if (ord($c) == $b + ($b == 0x1d454 ? 2 : 1))
       {push $r[-1]->@*, $c;
       }
      else
       {push @r, [$c];
       }
     }

    if ($z)                                                                     # Write ranges ready to load into zmm registers
     {for my $i(keys @r)
       {my $r = $r[$i];
        my $j = $i + 1;
        my $s = ord($$r[0]);                                                    # Start of range
        my $l = ord($$r[-1]);                                                   # End of range
        say STDERR "Range $j: ",
          sprintf("0x%x to 0x%x length: %2d", $s, $l, $l - $s + 1);
        push @range, [$z,  $s, $l];
        push @zmm,   [$z, $$Lexicals{$z}->number, $s, $l];
       }
     }
   }

  if (1)                                                                        # Load special ranges
   {my $s = ord $Tables->semiColon;
    my $t = ord $Tables->separator;
    my %l = map {$_ => $Lexicals->{$_}->number}  keys $Lexicals->%*;            # Ennumerate lexical items
    my $nl = ord("\n");

#               0               1                              2    3
    push @zmm, ["NewLine",      $l{NewLine},                   $nl, $nl];
    push @zmm, ["Ascii",        $l{Ascii},                     0,   127];
    push @zmm, ["semiColon",    $Lexicals->semiColon->number,  $s,  $s];
    push @zmm, ["WhiteSpace",   $Lexicals->WhiteSpace->number, $t,  $t];
    @zmm = sort {$$a[3] <=> $$b[3]} @zmm;
   }

  $Tables->alphabetRanges = scalar(@zmm);                                       # Alphabet ranges
  lll "Alphabet Ranges: ",  scalar(@zmm);
  say STDERR formatTable(\@zmm);

  if (1)                                                                        # Write zmm load sequence
   {my @l; my @h; my %r;                                                        # Low, high, current start within range
    for my $r(@zmm)
     {my $l = $r{$$r[0]}//0;                                                    # Current start of range

      push @l, (($$r[1]<<24) + $$r[2]);                                         # Start of range in high and lexical item in low at byte 3 allows us to replace the utf32 code with XX....YY where XX is the lexical item type and YY is the position in the range of that lexical item freeing the two central bytes for other purposes
      push @h, (($l    <<24) + $$r[3]);
      $r{$$r[0]} += ($$r[3] - $$r[2]) + 1;                                      # Extend the base of the current range
     }

    push @l, 0 while @l < 16;                                                   # Clear remaining ranges
    push @h, 0 while @h < 16;
    my $l = join ', ', map {sprintf("0x%08x", $_)} @l;                          # Format zmm load sequence
    my $h = join ', ', map {sprintf("0x%08x", $_)} @h;
    say STDERR "Lexical Low / Lexical High:\n$l\n$h";
    $Tables->lexicalLow  = [@l];
    $Tables->lexicalHigh = [@h];
   }

  $Tables->alphabets = \%selected;
 }

sub brackets                                                                    # Write brackets
 {my @S;

  my @s = readFile $data;

  for my $s(@s)                                                                 # Select the brackets we want
   {next unless $s =~ m(;P[s|e];)i;                                             # Select brackets
    my @w = split m/;/, $s;

    my ($point, $name) = @w;
    my $u = eval "0x$point";
    $@ and confess "$s\n$@\n";

    next if $u <= 0x208e;
    next if $u >=  9121 and  $u <=  9137;
    next if $u >= 11778 and  $u <= 11815;
    next if $u >= 12300 and  $u <= 12303;
    next if $u >= 65047 and  $u <= 65118 ;
    next if $u >= 65378;

    next if $u >= 0x27C5 and $u <= 0x27C6;                                      # Bag
    next if $u >= 0x29D8 and $u <= 0x29D9;                                      # Wiggly fence
    next if $u >= 0x29DA and $u <= 0x29Db;                                      # Double Wiggly fence
    next if $u == 0x2E42;                                                       # Double Low-Reversed-9 Quotation Mark[1]
    next if $u >= 0x301D and $u <= 0x3020;                                      # Quotation marks

    push @S, [$u, $name, $s];
   }

  @S % 2 and confess "Mismatched bracket pairs";
  lll "Bracket Pairs: ", scalar(@S) / 2;

  my ($T, @T) = @S;                                                             # Divide into ranges
  push my @t, [$T];
  for my $t(@T)
   {my ($u, $point, $name) = @$t;
    if ($$T[0] + 1 == $u)
     {push $t[-1]->@*, $T = $t;
      next;
     }
    push @t, [$T = $t];
   }

  @t = grep {@$_ > 1} @t;                                                       # Remove small blocks so we can fit into one zmm

  if (1)                                                                        # Bracket strings
   {my @o; my @c;
    my $i = 0;
    for   my $r(@t)
     {for my $t(@$r)
       {push @o, chr $$t[0] unless $i % 2;
        push @c, chr $$t[0] if     $i % 2;
        ++$i;
       }
     }

    if (0)                                                                      # Print every bracket pair
     {for my $i(keys @o)
       {lll "Bracket pair $i", $o[$i], $c[$i], ord($o[$i]), ord($c[$i]);
       }
     }

    $Tables->bracketsOpen  = [@o];                                              # Brackets list
    $Tables->bracketsClose = [@c];
   }

  my @l; my @h;
  my $index = $bracketBase;
  for my $t(@t)                                                                 # Load zmm0, zmm1
   {if (@$t > 1)
     {push @l, sprintf("0x%08x", $$t[0] [0] + ($index<<24));
      $index += scalar(@$t) - 1;
      push @h, sprintf("0x%08x", $$t[-1][0] + ($index<<24));
     }
    elsif ($$t[-1][-1] =~ m(LEFT))                                              # Single range left
     {++$index if $index % 2;
      push @l, sprintf("0x%08x", $$t[0] [0] + ($index<<24));
      push @h, sprintf("0x%08x", $$t[0] [0] + ($index<<24));
     }
    else                                                                        # Single range right
     {++$index unless $index % 2;
      push @l, sprintf("0x%08x", $$t[0] [0] + ($index<<24));
      push @h, sprintf("0x%08x", $$t[0] [0] + ($index<<24));
     }
    $index += 1;
   }
  push @l, 0 while @l < 16;
  push @h, 0 while @h < 16;
  $Tables->brackets = @l;                                                       # Number of brackets
  lll "Bracket ranges: ", scalar(@l);

  my $L = join '', join(', ',  @l);
  my $H = join '', join(', ',  @h);
  say STDERR "$L\n$H";

  $Tables->bracketsLow  = [@l];
  $Tables->bracketsHigh = [@h];
 }

sub transitions                                                                 # Write transitions table
 {my @l;
  for my $l(sort keys $Lexicals->%*)                                            # Each lexical item that could appear in a transition becuase it is like a Tree::Term
   {my $L = $$Lexicals{$l};
    next unless $L->letter;
    push @l, $L;
   }


  my %t;
  for my $l1(@l)                                                                # Transitions on numbers
   {my $a = $l1->letter;
    my $A = Tree::Term::LexicalStructure()->codes->{$a}->next;                         # The permitted transitions as letters

    for my $l2(@l)
     {my $b = $l2->letter;
      next unless index($A, $b) > -1;
      $t{$l1->number}{$l2->number} = "$a$b";
     }
   }

  if (1)                                                                        # Load transitions
   {my $n = 0;                                                                  # Number of transitions
    my @t;                                                                      # Transitions
    for   my $t1(keys %t)
     {for my $t2(keys $t{$t1}->%*)
       {++$n;
        push @t, $t1 + ($t2 << 4);                                              # Encode transitions
       }
     }
    lll "Number of transitions = $n";
    $Tables->transitions = [@t];
    lll join ', ', map{sprintf("0x%02x", $_)} @t;
   }
 }

sub translateSomeText($)                                                        # Translate some text
 {my ($s) = @_;                                                                 # String to translate
  my @w = (substr($s, 0, 1));
  for my $i(1..length($s))                                                      # Parse into strings of letters and spaces
   {my $b = join '', sort split //, substr($s, $i - 1, 2) =~ s(\S) (a)gsr  =~ s(\s) ( )gsr;
    my $c = substr($s, $i, 1);
    if ($b ne ' a')
     {$w[-1] .= $c;
     }
    else
     {push @w, $c;
     }
   }

  my %alphabets;                                                                # Alphabets for each lexical

  for my $l(keys $TreeTermLexicals->%*)
   {my $m = $TreeTermLexicals->{$l};
    my $n = $Tables->lexicalAlpha->{$m}[0];
    next unless $n;
    my $a = $Tables->alphabets->{$n};
    next unless $a;
    $alphabets{$l} = [$n, $a];
   }

  my $T = '';
  my $normal = join '', 'A'..'Z', 'a'..'z';                                     # The alphabet we can write lexical items

  my sub translate($)                                                           # Translate a string written in normal into the indicated alphabet
   {my ($lexical) = @_;                                                         # Lexical item to translate

    my $a =  $alphabets{substr($lexical, 0, 1)};                                # Alphabet to translate to
    my @a =   split //, $$a[1];                                                 # Alphabet to translate to
    for my $c(split //, substr($lexical, 1))
     {my $i = index $normal, $c;
      if ($$a[0] =~ m(\AmathematicalItalic\Z))
       {$c eq 'h' and confess "Cannot translate 'h' to $$a[0]";
        --$i if $c ge 'h';
       }
      $T .= $a[$i];
     }
   }

  for my $w(@w)
   {my $t = substr($w, 0, 1); my $r = substr($w, 1);
    if ($t =~ m(\A(a|d|v)\Z)) {translate $w}
    elsif ($t eq 's')         {$T .= $Tables->alphabets->{semiColon}}
    elsif ($t eq 'b')         {$T .= $Tables->bracketsOpen ->[$r]}
    elsif ($t eq 'B')         {$T .= $Tables->bracketsClose->[$r]}
    elsif ($t =~ m(\n))       {$T .= $w}
    elsif ($t =~ m(\s))       {$T .= $Tables->separator x length($w)}
    elsif ($t eq 'A')         {$T .= substr($w, 1) =~ s(-) (\n)gsr}
    else {confess "Invalid lexical item $s"}
   }

  lll "Sample text length in chars:", sprintf("0x%x", length($T));

  if (0)                                                                        # Print source code as utf8
   {my @T = split //, $T;
    for my $i(keys @T)
     {my $c = $T[$i];
      say STDERR "$i   $c ", sprintf("%08x   %08x", ord($c), convertUtf32ToUtf8(ord($c)));
     }
   }

  lll "Sample text:\n$T";
  $Tables->sampleText = $T;
 }

alphabets;                                                                      # Locate alphabets
brackets;                                                                       # Locate brackets
transitions;                                                                    # Transitions table
translateSomeText <<END;                                                        # Translate some text into Nida
va aassign b1 b2 b3 vbp B3 B2 dplus b4 vsc B4 B1 s
vaa aassign
  Asome--ascii--text dplus
  vcc s
END

owf $lexicalsFile, dump($Tables);                                               # Write results

__DATA__
CIRCLED LATIN LETTER  : ‚í∂‚í∑‚í∏‚íπ‚í∫‚íª‚íº‚íΩ‚íæ‚íø‚ìÄ‚ìÅ‚ìÇ‚ìÉ‚ìÑ‚ìÖ‚ìÜ‚ìá‚ìà‚ìâ‚ìä‚ìã‚ìå‚ìç‚ìé‚ìè‚ìê‚ìë‚ìí‚ìì‚ìî‚ìï‚ìñ‚ìó‚ìò‚ìô‚ìö‚ìõ‚ìú‚ìù‚ìû‚ìü‚ì†‚ì°‚ì¢‚ì£‚ì§‚ì•‚ì¶‚ìß‚ì®‚ì©
MATHEMATICAL BOLD  : ùêÄùêÅùêÇùêÉùêÑùêÖùêÜùêáùêàùêâùêäùêãùêåùêçùêéùêèùêêùêëùêíùêìùêîùêïùêñùêóùêòùêôùêöùêõùêúùêùùêûùêüùê†ùê°ùê¢ùê£ùê§ùê•ùê¶ùêßùê®ùê©ùê™ùê´ùê¨ùê≠ùêÆùêØùê∞ùê±ùê≤ùê≥ùö®ùö©ùö™ùö´ùö¨ùö≠ùöÆùöØùö∞ùö±ùö≤ùö≥ùö¥ùöµùö∂ùö∑ùö∏ùö∫ùöªùöºùöΩùöæùöøùõÄùõÇùõÉùõÑùõÖùõÜùõáùõàùõâùõäùõãùõåùõçùõéùõèùõêùõëùõíùõîùõïùõñùõóùõòùõôùõöùüäùüã
MATHEMATICAL BOLD FRAKTUR  : ùï¨ùï≠ùïÆùïØùï∞ùï±ùï≤ùï≥ùï¥ùïµùï∂ùï∑ùï∏ùïπùï∫ùïªùïºùïΩùïæùïøùñÄùñÅùñÇùñÉùñÑùñÖùñÜùñáùñàùñâùñäùñãùñåùñçùñéùñèùñêùñëùñíùñìùñîùñïùññùñóùñòùñôùñöùñõùñúùñùùñûùñü
MATHEMATICAL BOLD ITALIC  : ùë®ùë©ùë™ùë´ùë¨ùë≠ùëÆùëØùë∞ùë±ùë≤ùë≥ùë¥ùëµùë∂ùë∑ùë∏ùëπùë∫ùëªùëºùëΩùëæùëøùíÄùíÅùíÇùíÉùíÑùíÖùíÜùíáùíàùíâùíäùíãùíåùíçùíéùíèùíêùíëùííùíìùíîùíïùíñùíóùíòùíôùíöùíõùúúùúùùúûùúüùú†ùú°ùú¢ùú£ùú§ùú•ùú¶ùúßùú®ùú©ùú™ùú´ùú¨ùúÆùúØùú∞ùú±ùú≤ùú≥ùú¥ùú∂ùú∑ùú∏ùúπùú∫ùúªùúºùúΩùúæùúøùùÄùùÅùùÇùùÉùùÑùùÖùùÜùùàùùâùùäùùãùùåùùçùùé
MATHEMATICAL BOLD SCRIPT  : ùìêùìëùìíùììùìîùìïùìñùìóùìòùìôùìöùìõùìúùìùùìûùìüùì†ùì°ùì¢ùì£ùì§ùì•ùì¶ùìßùì®ùì©ùì™ùì´ùì¨ùì≠ùìÆùìØùì∞ùì±ùì≤ùì≥ùì¥ùìµùì∂ùì∑ùì∏ùìπùì∫ùìªùìºùìΩùìæùìøùîÄùîÅùîÇùîÉ
MATHEMATICAL DOUBLE-STRUCK  : ùî∏ùîπùîªùîºùîΩùîæùïÄùïÅùïÇùïÉùïÑùïÜùïäùïãùïåùïçùïéùïèùïêùïíùïìùïîùïïùïñùïóùïòùïôùïöùïõùïúùïùùïûùïüùï†ùï°ùï¢ùï£ùï§ùï•ùï¶ùïßùï®ùï©ùï™ùï´
MATHEMATICAL FRAKTUR  : ùîÑùîÖùîáùîàùîâùîäùîçùîéùîèùîêùîëùîíùîìùîîùîñùîóùîòùîôùîöùîõùîúùîûùîüùî†ùî°ùî¢ùî£ùî§ùî•ùî¶ùîßùî®ùî©ùî™ùî´ùî¨ùî≠ùîÆùîØùî∞ùî±ùî≤ùî≥ùî¥ùîµùî∂ùî∑
MATHEMATICAL ITALIC  : ùê¥ùêµùê∂ùê∑ùê∏ùêπùê∫ùêªùêºùêΩùêæùêøùëÄùëÅùëÇùëÉùëÑùëÖùëÜùëáùëàùëâùëäùëãùëåùëçùëéùëèùëêùëëùëíùëìùëîùëñùëóùëòùëôùëöùëõùëúùëùùëûùëüùë†ùë°ùë¢ùë£ùë§ùë•ùë¶ùëßùõ¢ùõ£ùõ§ùõ•ùõ¶ùõßùõ®ùõ©ùõ™ùõ´ùõ¨ùõ≠ùõÆùõØùõ∞ùõ±ùõ≤ùõ¥ùõµùõ∂ùõ∑ùõ∏ùõπùõ∫ùõºùõΩùõæùõøùúÄùúÅùúÇùúÉùúÑùúÖùúÜùúáùúàùúâùúäùúãùúåùúéùúèùúêùúëùúíùúìùúî
MATHEMATICAL MONOSPACE  : ùô∞ùô±ùô≤ùô≥ùô¥ùôµùô∂ùô∑ùô∏ùôπùô∫ùôªùôºùôΩùôæùôøùöÄùöÅùöÇùöÉùöÑùöÖùöÜùöáùöàùöâùöäùöãùöåùöçùöéùöèùöêùöëùöíùöìùöîùöïùöñùöóùöòùöôùööùöõùöúùöùùöûùöüùö†ùö°ùö¢ùö£
MATHEMATICAL SANS-SERIF  : ùñ†ùñ°ùñ¢ùñ£ùñ§ùñ•ùñ¶ùñßùñ®ùñ©ùñ™ùñ´ùñ¨ùñ≠ùñÆùñØùñ∞ùñ±ùñ≤ùñ≥ùñ¥ùñµùñ∂ùñ∑ùñ∏ùñπùñ∫ùñªùñºùñΩùñæùñøùóÄùóÅùóÇùóÉùóÑùóÖùóÜùóáùóàùóâùóäùóãùóåùóçùóéùóèùóêùóëùóíùóì
MATHEMATICAL SANS-SERIF BOLD  : ùóîùóïùóñùóóùóòùóôùóöùóõùóúùóùùóûùóüùó†ùó°ùó¢ùó£ùó§ùó•ùó¶ùóßùó®ùó©ùó™ùó´ùó¨ùó≠ùóÆùóØùó∞ùó±ùó≤ùó≥ùó¥ùóµùó∂ùó∑ùó∏ùóπùó∫ùóªùóºùóΩùóæùóøùòÄùòÅùòÇùòÉùòÑùòÖùòÜùòáùùñùùóùùòùùôùùöùùõùùúùùùùùûùùüùù†ùù°ùù¢ùù£ùù§ùù•ùù¶ùù®ùù©ùù™ùù´ùù¨ùù≠ùùÆùù∞ùù±ùù≤ùù≥ùù¥ùùµùù∂ùù∑ùù∏ùùπùù∫ùùªùùºùùΩùùæùùøùûÄùûÇùûÉùûÑùûÖùûÜùûáùûà
MATHEMATICAL SANS-SERIF BOLD ITALIC  : ùòºùòΩùòæùòøùôÄùôÅùôÇùôÉùôÑùôÖùôÜùôáùôàùôâùôäùôãùôåùôçùôéùôèùôêùôëùôíùôìùôîùôïùôñùôóùôòùôôùôöùôõùôúùôùùôûùôüùô†ùô°ùô¢ùô£ùô§ùô•ùô¶ùôßùô®ùô©ùô™ùô´ùô¨ùô≠ùôÆùôØùûêùûëùûíùûìùûîùûïùûñùûóùûòùûôùûöùûõùûúùûùùûûùûüùû†ùû¢ùû£ùû§ùû•ùû¶ùûßùû®ùû™ùû´ùû¨ùû≠ùûÆùûØùû∞ùû±ùû≤ùû≥ùû¥ùûµùû∂ùû∑ùû∏ùûπùû∫ùûºùûΩùûæùûøùüÄùüÅùüÇ
MATHEMATICAL SANS-SERIF ITALIC  : ùòàùòâùòäùòãùòåùòçùòéùòèùòêùòëùòíùòìùòîùòïùòñùòóùòòùòôùòöùòõùòúùòùùòûùòüùò†ùò°ùò¢ùò£ùò§ùò•ùò¶ùòßùò®ùò©ùò™ùò´ùò¨ùò≠ùòÆùòØùò∞ùò±ùò≤ùò≥ùò¥ùòµùò∂ùò∑ùò∏ùòπùò∫ùòª
MATHEMATICAL SCRIPT  : ùíúùíûùíüùí¢ùí•ùí¶ùí©ùí™ùí´ùí¨ùíÆùíØùí∞ùí±ùí≤ùí≥ùí¥ùíµùí∂ùí∑ùí∏ùíπùíªùíΩùíæùíøùìÄùìÅùìÇùìÉùìÖùìÜùìáùìàùìâùìäùìãùìåùìçùìéùìè
NEGATIVE CIRCLED LATIN LETTER  : üÖêüÖëüÖíüÖìüÖîüÖïüÖñüÖóüÖòüÖôüÖöüÖõüÖúüÖùüÖûüÖüüÖ†üÖ°üÖ¢üÖ£üÖ§üÖ•üÖ¶üÖßüÖ®üÖ©
NEGATIVE SQUARED LATIN LETTER  : üÖ∞üÖ±üÖ≤üÖ≥üÖ¥üÖµüÖ∂üÖ∑üÖ∏üÖπüÖ∫üÖªüÖºüÖΩüÖæüÖøüÜÄüÜÅüÜÇüÜÉüÜÑüÜÖüÜÜüÜáüÜàüÜâ
SQUARED LATIN LETTER  : üÑ∞üÑ±üÑ≤üÑ≥üÑ¥üÑµüÑ∂üÑ∑üÑ∏üÑπüÑ∫üÑªüÑºüÑΩüÑæüÑøüÖÄüÖÅüÖÇüÖÉüÖÑüÖÖüÖÜüÖáüÖàüÖâüÜ•
